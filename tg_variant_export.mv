<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "tg_variant_export">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Export Variants to CSV or XML">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:features"	VALUE = "export">
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	This portion actually loops through the data you want to export.
	|	The Progress bar is started/ updated/ completed here.
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT g.Variant_Export_Continue }">
		<MvIF EXPR = "{ fexists( '/' $ g.VariantsExport_File ) }">
			<MvASSIGN NAME = "l.success" VALUE = "{ fdelete( '/' $ g.VariantsExport_File ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( g.FormatType EQ 'CSV' ) AND ( ISNULL g.Variant_Progress ) }">
		<MvASSIGN NAME = "l.csv_header" VALUE = "{ CSV_Template_Header() }">
		<MvASSIGN NAME = "l.success" VALUE = "{ file_create( '/' $ g.VariantsExport_File, 'data', l.csv_header ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'Variants Export Progress', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">

	<MvIF EXPR = "{ g.Total_Variant_Count }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Variant_Progress, g.Total_Variant_Count ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Load_Variants_Offset( g.Variant_Progress, 250, g.Total_Variant_Count, l.variants ) }">

	<MvFOREACH ITERATOR = "l.variant" ARRAY = "l.variants">
		<MvASSIGN NAME = "l.curr_output" VALUE = "">

		<MvIF EXPR = "{ g.FormatType EQ 'XML' }">
			<MvEVAL EXPR = "{ XML_Template_ProductVariant_Add( l.variant:master_code, l.variant:options, l.variant:parts, l.variant:pricing, l.curr_output) }">
		<MvELSE>
			<MvEVAL EXPR = "{ CSV_Template_ProductVariant_Add( l.variant:master_code, l.variant:options, l.variant:parts, l.variant:pricing, l.curr_output) }">
		</MvIF>

		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.curr_output }">

		<MvASSIGN NAME = "g.Variant_Progress" VALUE = "{ g.Variant_Progress + 1 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Variant_Progress, g.Total_Variant_Count ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ fexists( '/' $ g.VariantsExport_File ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ file_append( '/' $ g.VariantsExport_File, 'data', l.output ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.success" VALUE = "{ file_create( '/' $ g.VariantsExport_File, 'data', l.output ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Variant_Progress GE g.Total_Variant_Count }">

		<MvIF EXPR = "{ g.VariantsExport_SendEmail }">
			<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ Send_Email_Attachment( g.VariantsExport_Email, g.VariantsExport_Email, '', 'Variant Export', g.VariantsExport_File, '/', 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'TGVE-SUCCESS-1', 'Variants Export Email Sent' ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( 'Exported Variants.' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'TGVE-SUCCESS-2', 'Variants Exported to ' $ g.VariantsExport_File  ) }">

		<MvASSIGN NAME = "g.Variant_Export_Continue" VALUE = "0">
		<MvASSIGN NAME = "g.Variant_Progress" VALUE = "0">
	<MvELSE>
		<MvASSIGN NAME = "g.Variant_Export_Continue" VALUE = "1">
	</MvIF>

	<MvHIDE FIELDS = "	g.Variant_Export_Continue,
						g.FormatType,
						g.Module_Code,
						g.Variant_Progress,
						g.VariantsExport_File,
						g.VariantsExport_SendEmail,
						g.VariantsExport_Email,
						g.Total_Variant_Count">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.VariantsExport_File"	VALUE = "{ trim( g.VariantsExport_File ) }">

	<MvIF EXPR = "{ len( g.VariantsExport_File ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'VariantsExport_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.VariantsExport_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'VariantsExport_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "g.VariantsExport_Email" 	VALUE = "{ trim( g.VariantsExport_Email ) }">

	<MvIF EXPR = "{ g.VariantsExport_SendEmail }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.VariantsExport_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'VariantsExport_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ISNULL g.VariantsExport_File AND g.FormatType EQ 'CSV' }">
		<MvASSIGN NAME = "g.VariantsExport_File" VALUE = "variants.csv">
	<MvELSEIF EXPR = "{ ISNULL g.VariantsExport_File }">
		<MvASSIGN NAME = "g.VariantsExport_File" VALUE = "variants.xml">
	</MvIF>

	<MvIF EXPR = "{ g.Variant_Export_Continue EQ 1 }">
		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export Variants to CSV or XML', 'EXPT', '' ) }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting Variants...', 'EXPT', 'EXPT' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export Variants to CSV or XML', 'EXPT', '' ) }">
		<MvDO FILE = "{ g.Module_Admin }" NAME = "l.ok" VALUE = "{ BeginContent() }">

			<div style="font-family: sans-serif;">
				<MvHIDE FIELDS = "g.Module_Code">
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Export Format', 150 ) }">
					<table cellspacing="5">
						<tr>
							<td valign="top"><strong>Export Variants to File:</strong></td>
							<td width="100%">
								<input type="text" name="VariantsExport_File" id="jsVariantsExport_File" size="40" value="{ encodeentities( g.VariantsExport_File ) }"><br />
								<em>This is located in the private directory. This will be deleted if it already exists.</em>
							</td>
						</tr>
						<tr>
							<td valign="top"><strong>File Format:</strong></td>
							<td width="100%">
								<select name="FormatType" id="jsFormatType">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'XML', g.FormatType, 'XML' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'CSV', g.FormatType, 'CSV' ) }">
								</select>
							</td>
						</tr>
						<tr>
							<td valign="top">Email File To:</td>
							<td width="100%">
								<MvIF EXPR = "{ NOT g.VariantsExport_SendEmail }">
									<label><input type="radio" name="VariantsExport_SendEmail" value="0" checked>Do Not Email<br></label>
									<input type="radio" name="VariantsExport_SendEmail" value="1">
								<MvELSE>
									<label><input type="radio" name="VariantsExport_SendEmail" value="0">Do Not Email<br></label>
									<input type="radio" name="VariantsExport_SendEmail" value="1" checked>
								</MvIF>

								<input type="text" size="40" name="VariantsExport_Email" value="{ encodeentities( g.Store:email ) }">
							</td>
						</tr>
					</table>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</div>
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">
		<script type="text/javascript">
			var format_type = document.getElementById( 'jsFormatType' );
			var export_file = document.getElementById( 'jsVariantsExport_File' );
			format_type.onchange = function(){
				var elem = (typeof this.selectedIndex === "undefined" ? window.event.srcElement : this);
				var value = elem.value || elem.options[elem.selectedIndex].value;
				if ( value == 'CSV' ){
					export_file.value = 'variants.csv';
				} else {
					export_file.value = 'variants.xml';
				} 
			};
		</script>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Variants_Offset" PARAMETERS = "offset var, limit, total_variants var, variants var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'pv.product_id as product_id, pv.variant_id as variant_id' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.query, g.Store_Table_Prefix $ 'ProductVariantParts', 'pv' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.query, 'pv.product_id, pv.variant_id' ) }">
	<MvASSIGN NAME = "l.built_query"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.query, l.search_fields ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.count_query, '1 AS total_count' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.count_query, g.Store_Table_Prefix $ 'ProductVariantParts', 'pv' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.count_query, 'pv.product_id, pv.variant_id' ) }">
	<MvASSIGN NAME = "l.count_sql"	VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.count_query, l.count_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Count( l.count_sql, l.count_fields, l.total_variants ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGVE-ERROR-2', g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'Load_Variants', l.built_query, l.fields, l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGVE-ERROR-3', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.variant_count" VALUE = "0">
	<MvASSIGN NAME = "l.counter" VALUE = "1">

	<MvWHILE EXPR = "{ ( NOT Load_Variants.d.EOF ) AND ( ( l.limit EQ 0 ) OR ( l.variant_count LT l.limit ) ) }">
		<MvASSIGN NAME = "l.product"		VALUE = "">
		<MvASSIGN NAME = "l.parts"			VALUE = "">
		<MvASSIGN NAME = "l.options"		VALUE = "">
		<MvASSIGN NAME = "l.variant_parts"	VALUE = "">

		<MvASSIGN NAME = "l.master_id" VALUE = "{ Load_Variants.d.product_id }">
		<MvASSIGN NAME = "l.variant_id" VALUE = "{ Load_Variants.d.variant_id }">

		<MvASSIGN NAME = "l.variants" INDEX = "{ l.counter }" MEMBER = "master_id" VALUE = "{ l.master_id }">
		<MvASSIGN NAME = "l.variants" INDEX = "{ l.counter }" MEMBER = "variant_id" VALUE = "{ l.variant_id }">


		<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_ID( l.master_id, l.product )  }">
			<MvASSIGN NAME = "l.variants" INDEX = "{ l.counter }" MEMBER = "master_code" VALUE = "{ l.product:code }">
		</MvIF>

		<MvIF EXPR = "{ [ g.Module_Library_DB ].ProductVariantPartList_Load_Variant( l.master_id, l.variant_id, l.variant_parts  ) }">
			<MvASSIGN NAME = "l.variants" INDEX = "{ l.counter }" MEMBER = "parts" VALUE = "{ Read_Variant_Parts( l.variant_parts ) }">
		</MvIF>

		<MvIF EXPR = "{ [ g.Module_Library_DB ].ProductVariantPricing_Load_Variant( l.master_id, l.variant_id, l.variant_pricing ) }">
		</MvIF>
		<MvASSIGN NAME = "l.variants" INDEX = "{ l.counter }" MEMBER = "pricing" VALUE = "{ Read_Variant_Pricing( l.variant_pricing ) }">

		<MvIF EXPR = "{ [ g.Module_Library_DB ].ProductVariantList_Load_Variant( l.master_id, l.variant_id, l.options ) }">
			<MvASSIGN NAME = "l.variants" INDEX = "{ l.counter }" MEMBER = "options" VALUE = "{ Read_Variant_Options( l.options ) }">
		</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		<MvASSIGN NAME = "l.variant_count" VALUE = "{ l.variant_count + 1 }">
		<MvSKIP NAME = "Merchant" VIEW = "Load_Variants" ROWS = 1>
	</MvWHILE>
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Load_Variants">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "XML_Template_ProductVariant_Add" PARAMETERS = "master_code, options var, parts var, pricing var, output var" STANDARDOUTPUTLEVEL = "">

	<MvCOMMENT>
	|
	|	XML Template for 1 Product Variant.
	|		Includes Default Product Variant Tag as well
	|	Line breaks & Tabs includes
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.break" VALUE = "{ asciichar( 10 ) }">
	<MvASSIGN NAME = "l.tab" VALUE = "{ asciichar( 9 ) }">
	<MvIF EXPR = "{ miva_array_elements( l.options ) GT 0 }">
		<MvASSIGN NAME = "l.output" VALUE = "{ '<ProductVariant_Add product_code=\"' $ l.master_code $ '\">' $ l.break }" />
	<MvELSE>
		<MvASSIGN NAME = "l.output" VALUE = "{ '<ProductVariant_Update_Default product_code=\"' $ l.master_code $ '\">' $ l.break }" />
	</MvIF>

		<MvCOMMENT>
		|
		|	Options
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ miva_array_elements( l.options ) GT 0 }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ '<Options>' $ l.break }" />
				<MvFOREACH ITERATOR = "l.option" ARRAY = "l.options">
					<MvIF EXPR = "{ l.option:attribute:attemp_id GT 0 }">
						<MvIF EXPR = "{ ISNULL l.option:option:code }">
							<MvIF EXPR = "{ l.option:option_id EQ 1 }">
								<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<AttributeTemplateAttribute_Boolean attribute_code=\"' $ l.option:attribute:code $ '\" attributetemplateattribute_code=\"' $ l.option:product_attribute:code $ '\" present=\"true\" />' $ l.break }">
							<MvELSE>
								<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<AttributeTemplateAttribute_Boolean attribute_code=\"' $ l.option:attribute:code $ '\" attributetemplateattribute_code=\"' $ l.option:product_attribute:code $ '\" present=\"false\" />' $ l.break }">
							</MvIF>
						<MvELSE>
							<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<AttributeTemplateAttribute_Option attribute_code=\"' $ l.option:attribute:code $ '\" attributetemplateattribute_code=\"' $ l.option:product_attribute:code $ '\" option_code=\"' $ l.option:option:code $ '\" />' $ l.break }">
						</MVIF>
					<MvELSE>
						<MvIF EXPR = "{ ISNULL l.option:option:code }">
							<MvIF EXPR = "{ l.option:option_id EQ 1 }">
								<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Attribute_Boolean attribute_code=\"' $ l.option:attribute:code $ '\" present=\"true\" />' $ l.break }">
							<MvELSE>
								<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Attribute_Boolean attribute_code=\"' $ l.option:attribute:code $ '\" present=\"false\" />' $ l.break }">
							</MvIF>
						<MvELSE>
							<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Attribute_Option attribute_code=\"' $ l.option:attribute:code $ '\" option_code=\"' $ l.option:option:code $ '\" />' $ l.break }">
						</MVIF>
					</MvIF>
				</MvFOREACH>
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ '</Options>' $ l.break }" />
		</MvIF>

		<MvCOMMENT>
		|
		|	Parts
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ '<Parts>' $ l.break }" />
			<MvFOREACH ITERATOR = "l.part" ARRAY = "l.parts">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Part product_code=\"' $ l.part:code $ '\" quantity=\"' $ l.part:quantity $ '\" />' $ l.break }">
			</MvFOREACH>
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ '</Parts>' $ l.break }" />

		<MvCOMMENT>
		|
		|	Pricing
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ '<ProductVariantPricing>' $ l.break }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Method>'	$ l.pricing:method_name	$	'</Method>'	$ l.break }">
			<MvIF EXPR = "{ NOT ISNULL l.pricing:price }">	<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Price>'		$ l.pricing:price		$	'</Price>'	$ l.break }"></MvIF>
			<MvIF EXPR = "{ NOT ISNULL l.pricing:cost }">	<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Cost>'		$ l.pricing:cost		$	'</Cost>'	$ l.break }"></MVIF>
			<MvIF EXPR = "{ NOT ISNULL l.pricing:weight }">	<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ l.tab $ '<Weight>'	$ l.pricing:weight		$	'</Weight>'	$ l.break }"></MvIF>
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.tab $ '</ProductVariantPricing>' $ l.break }">

	<MvIF EXPR = "{ miva_array_elements( l.options ) GT 0 }">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '</ProductVariant_Add>' $ l.break }" />
	<MvELSE>
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ '</ProductVariant_Update_Default>' $ l.break }" />
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Read_Variant_Parts" PARAMETERS = "parts var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.current_parts" VALUE = "{ l.parts }">
	<MvFOREACH ITERATOR = "l.part" ARRAY = "l.current_parts" INDEX = "l.parts_index">
		<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_ID( l.part:part_id, l.part:product ) }">
			<MvASSIGN NAME = "l.new_parts" INDEX = "{ l.parts_index }" MEMBER = "code"			VALUE = "{ l.part:product:code }">
			<MvASSIGN NAME = "l.new_parts" INDEX = "{ l.parts_index }" MEMBER = "product_id"	VALUE = "{ l.part:product:product_id }">
			<MvASSIGN NAME = "l.new_parts" INDEX = "{ l.parts_index }" MEMBER = "part_id"		VALUE = "{ l.part:part_id }">
			<MvASSIGN NAME = "l.new_parts" INDEX = "{ l.parts_index }" MEMBER = "quantity"		VALUE = "{ l.part:quantity }">
		</MvIF>
	</MvFOREACH>
	<MvIF EXPR = "{ l.new_parts }">
		<MvFUNCTIONRETURN VALUE = "{ l.new_parts }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ l.parts }">
</MvFUNCTION>

<MvFUNCTION NAME = "Read_Variant_Pricing" PARAMETERS = "pricing var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.pricing:method EQ 1 }">
		<MvASSIGN NAME = "l.pricing" MEMBER = "method_name" VALUE = "specific">
	<MvELSEIF EXPR = "{ l.pricing:method EQ 2 }">
		<MvASSIGN NAME = "l.pricing" MEMBER = "method_name" VALUE = "sum">
	<MvELSE>
		<MvASSIGN NAME = "l.pricing" MEMBER = "method_name" VALUE = "master">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = "{ l.pricing }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Read_Variant_Options" PARAMETERS = "options var" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.option" ARRAY = "l.options" INDEX = "l.options_index">
		<MvIF EXPR = "{ l.option:attmpat_id GT 0 }">
			<MvDO FILE = "{ g.Module_Library_DB }"		NAME = "l.success" VALUE = "{ Attribute_Load_ID( l.option:attr_id, l.option:attribute ) }">
			<MvDO FILE = "{ g.Module_Feature_ATT_DB }"	NAME = "l.success" VALUE = "{ AttributeTemplateAttr_Load_ID( l.option:attmpat_id, l.option:product_attribute ) }">
			<MvDO FILE = "{ g.Module_Feature_ATT_DB }"	NAME = "l.success" VALUE = "{ AttributeTemplate_Load_ID( l.option:attribute:attemp_id, l.option:attributetemplate ) }">
			<MvDO FILE = "{ g.Module_Feature_ATT_DB }"	NAME = "l.success" VALUE = "{ AttributeTemplateOption_Load_ID( l.option:option_id, l.option:option ) }">
		<MvELSE>
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ Attribute_Load_ID( l.option:attr_id, l.option:attribute ) }">
			<MvDO FILE = "{ g.Module_Library_DB }" NAME = "l.success" VALUE = "{ Option_Load_ID( l.option:option_id, l.option:option ) }">
		</MvIF>
	</MvFOREACH>
	<MvFUNCTIONRETURN VALUE = "{ l.options }" />
</MvFUNCTION>

<MvFUNCTION NAME = "CSV_Template_Header" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.delim" VALUE = "{ asciichar( 44 ) }">
	<MvFUNCTIONRETURN VALUE = "{ 'MASTER_PRODUCT_CODE' $ l.delim $ 'PART_PRODUCT_CODE' $ l.delim $ 'ATTRIBUTE_CODE' $ l.delim $ 'OPTION_CODE' }">
</MvFUNCTION>

<MvFUNCTION NAME = "CSV_Template_ProductVariant_Add" PARAMETERS = "master_code, options var, parts var, pricing var, output var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		MASTER_PRODUCT_CODE
		PART_PRODUCT_CODE
		ATTRIBUTE_CODE
		OPTION_CODE
	</MvCOMMENT>

	<MvIF EXPR = "{ miva_array_elements( l.options ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.delim" VALUE = "{ asciichar( 44 ) }">
	<MvASSIGN NAME = "l.break" VALUE = "{ asciichar( 10 ) }">
	<MvASSIGN NAME = "l.output" VALUE = "">

	<MvFOREACH ITERATOR = "l.part" ARRAY = "l.parts">

		<MvFOREACH ITERATOR = "l.option" ARRAY = "l.options">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ l.break }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( l.master_code,				l.delim ) $ l.delim }">
			<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( l.part:code,					l.delim ) $ l.delim }">
			<MvIF EXPR = "{ l.option:attribute:attemp_id GT 0 }">
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( l.option:product_attribute:code,		l.delim ) $ l.delim }">
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( l.option:attribute:code,				l.delim ) $ l.delim }">
			</MvIF>
			<MvIF EXPR = "{ ISNULL l.option:option:code }">
				<MvIF EXPR = "{ l.option:option_id EQ 1 }">
					<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( 1, l.delim ) }">
				<MvELSE>
					<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( 0, l.delim ) }">
				</MvIF>
			<MvELSE>
				<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ miva_csv_encode( l.option:option:code, l.delim ) }">
			</MvIF>
		</MvFOREACH>

	</MvFOREACH>
</MvFUNCTION>